set term pdf size 2,3
set output "mcx1.pdf"
set encoding utf8

set multiplot layout 2,1 title "Multiclass CFRP R100"

set style fill solid border -1
set boxwidth 1.0 absolute
set style line 1 lt 1 lc rgb "#179c7d"
set style line 2 lt 1 lc rgb "#b1c800"
set style line 3 lt 1 lc rgb "#f29400"
set style line 4 lt 1 lc rgb "#006e92"
set style line 5 lt 1 lc rgb "#808080"

set lmargin 9
set rmargin 0
set key off

set xtics ('' 2, '' 7)
set xrange [-1:11]
set grid xtics ytics

###### R100 CM ############

set bmargin 0
set format x ""

set yrange [0:1]
set ytics 0,0.2,1
set ylabel "Safety margin (CM)" offset 0

plot 'mcx1.dat' using 1:3 title "CNN" with boxes ls 1, \
	'mcx1.dat' using 4:6 title "DNN" with boxes ls 2, \
	'mcx1.dat' using 7:9 title "SVM" with boxes ls 3, \
	'mcx1.dat' using 10:12 title "HMM" with boxes ls 4

###### R100 ER ############

set bmargin
set tmargin 0
set format x
set label "SIG" at 0.5,-.02 center
set label "PFA" at 4.5,-.02 center
set label "SFA" at 9.0,-.02 center

set yrange [0:0.19]
set ytics     ('0.02' 0.17)
set ytics add ('0.04' 0.15)
set ytics add ('0.06' 0.13)
set ytics add ('0.08' 0.11)
set ytics add ('0.1' 0.09)
set ytics add ('0.3' 0.07)
set ytics add ('0.5' 0.05)
set ytics add ('0.7' 0.03)
set ytics add ('0.9' 0.01)
set ylabel "Error rate (ER)" offset 1

plot 0.09 title "" with lines ls 5, \
    'mcx1.dat' using 1:(0.19-$2) title "CNN" with boxes ls 1, \
	'mcx1.dat' using 4:(0.19-$5) title "DNN" with boxes ls 2, \
	'mcx1.dat' using 7:(0.19-$8) title "SVM" with boxes ls 3, \
	'mcx1.dat' using 10:(0.19-$11) title "HMM" with boxes ls 4

unset multiplot

#pause mouse button2
